// 2021-02-17 14:33:55
#event create|Initialize the Gremlin
canActiveRegion = true;
scale = 1;

canHurt = true;
maxHp = 12;
hp = maxHp;
image_speed = 15/60;
state = WANDER;
stunned = false;

vsp = 0;
atkBox = 32; //divided by 2 on calculation.
damage = 5; //Attack damage.

hurtAlarm = 1;
friction = DEF_FRIC;

target = obj_pie; //objective priority.

objective = target;
stateLockAlarm = 2;
aggroRange = 16*8; //8 tiles
hAccel = 0;
vAccel = 0;
maxAccel = 2.5;
accelRate = 0.08;

stateLock = true;
alarm[stateLockAlarm] = 20;

image_xscale = sign(target.x-x)*scale;

event_inherited();
knockType = "FLY";
knockback_resistance = 0.1;
deathSound = snd_enemydie_ghost;
death_gib = part_nullGib;

if raid_boss = true
{
    scale = 3;
    maxAccel -= 0.5;
    knock_resistance += 0.25;
    deathSound = snd_enemydie_boss;
    maxHp = 50+waveScale(10,5,0,125);
    hp = maxHp
}

image_xscale = scale;
image_yscale = scale;

while place_meeting(x,y,OBSTA)
{ y -= 1; }

#event destroy
event_inherited();

if dropItem != true then exit;

var c = irandom(100);

if c <= 10 then scr_spawnEssence(1,10,1);//~10% chance
if worldControl.special_raidID == "SR_Shadow" && c >= 99 && insideView(x,y) == true
{
    scr_dropItem(ITEMID.weapon_subLimeMachineGun,0,ITEMTYPE.weapon ,x,y-4,noone); //~1% chance
    scr_playSound(snd_rareItem,false,8,x,y,1);
}

if raid_boss == true
{
    if c >= 80
    { scr_dropItem(ITEMID.acc_ultrablueStar,0,ITEMTYPE.accessory,x,y,noone); }
}

#event alarm1|Reset hurt
canHurt = true;
target = obj_player;
state = MOVE;
hAccel = 0;
vAccel = 0;

#event alarm2|Reset state lock
stateLock = false;

objective = target;
state = MOVE;

#event step
if canHurt == false then state = IDLE;
var canSeeTarget = !collision_line(x,y,target.x,target.y,OBSTA,false,false);

//Shadow Gremlin Ai

if stateLock == false
{    
    //Aggro range
    if target != obj_player
    {
        if distance_to_object(obj_player) <= aggroRange && obj_player.dead == false
        {
            target = obj_player;
        } else { target = obj_pie; }
    } else if obj_player.dead == true then { target = obj_pie; }
}

objective = target;

if instance_exists(objective) 
{ ai_shadowGremlin(state,spr_gremlinIdle,spr_gremlinJump,spr_gremlinWalk,1,objective,-4,atkBox); } 
else target = obj_pie;


//----Despawn Check-----------
if position_meeting(x,y,OBSTA) then instance_destroy();