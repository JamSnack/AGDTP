// 2020-12-14 13:32:46
#event create|Initialize the Pylon

canActiveRegion = false;
canHurt = true;
maxHp = 70+(25*floor(wave/5));
hp = maxHp;
image_speed = 8/60;
state = MOVE;
stunned = false;
scale = 1;

vsp = 0;
atkBox = (16*4); //divided by 2 on calculation.
damage = 5; //Attack damage.

hurtAlarm = 1;

target = obj_pie; //Target priority.

objective = target;
stateLockAlarm = 2;
aggroRange = 0;

stateLock = false;

//Flying speed
hAccel = 0;
vAccel = 0;
maxAccel = 1;
accelRate = 0.01;

//Animation
bobAmt = 0;
bobDir = 1;
bobLinearDir = 1;
bobRate = 0.001;
bobAltitude = 3;

event_inherited();

//Other Locals
spwnRate = 3-(0.5*floor(wave/3));
spwnRate = clamp(spwnRate,1,5);
alarm[3] = 1;
knockType = "FLY";
death_gib = part_nullGib;
knock_resistance = 0.5;

repeat(clamp(floor(wave/4),1,6))
{
    var a = instance_create(x,y,obj_sphereMinion);
    a.parentKing = id;
}

if raid_boss == true
{
    scale += 0.25; 
    maxAccel += 0.2;
    spwnRate-=0.25;
}

#event destroy
event_inherited();

if dropItem != true then exit;

var r = irandom(4);
if r == 1
{
    scr_dropItem(ITEMID.weapon_sphereLauncher,0,ITEMTYPE.weapon,x,y,noone);
    scr_playSound(snd_rareItem,false,11,x,y,1);
} else if r > 2
{
    scr_dropTag("Tool Speed+",1);
    scr_playSound(snd_rareItem,false,11,x,y,1);
}

scr_spawnEssence(1,60,5+floor(wave/3));

#event alarm1|Reset hurt
canHurt = true;
target = obj_player;

#event alarm2|Reset state lock
stateLock = false;

objective = target;
state = WANDER;
image_index = sign(objective.x-x);

#event alarm3|Create a Sphere Minion
var a = instance_create(x,y,obj_sphereMinion);
a.parentKing = id;
alarm[3] = room_speed*spwnRate;

#event step
//Pylon states
//TODO: If close to a playertile or something, enter WANDER. Like Bomb King.

ai_pylon(MOVE,spr_gremlinIdle,spr_gremlinWalk,1,obj_player,atkBox);
if speed != 0 then speed = approach(speed,0,1);

#event draw
draw_sprite_ext(sprite_index,image_index,x,y+(bobAmt*bobAltitude),image_xscale,image_yscale,image_angle,c_white,image_alpha);