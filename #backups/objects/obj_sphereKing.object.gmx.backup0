// 2021-05-13 15:00:23
#event create|Initialize the Pylon

canActiveRegion = false;
canHurt = true;
maxHp = 70+(25*floor(wave/5));
hp = maxHp;
image_speed = 8/60;
state = MOVE;
stunned = false;
scale = 1;

vsp = 0;
atkBox = (16*4); //divided by 2 on calculation.
damage = 10; //Attack damage.

hurtAlarm = 1;

target = obj_pie; //Target priority.

objective = target;
stateLockAlarm = 2;
aggroRange = 16*8;

stateLock = false;

//Flying speed
hAccel = 0;
vAccel = 0;
maxAccel = 3;
accelRate = 0.02;

//Animation
bobAmt = 0;
bobDir = 1;
bobLinearDir = 1;
bobRate = 0.001;
bobAltitude = 3;

event_inherited();

//Other Locals
spwnRate = 3-(0.5*floor(wave/3));
spwnRate = clamp(spwnRate,1,5);
alarm[3] = 1;
knockType = "FLY";
death_gib = part_nullGib;
knock_resistance = 3;
sphere_backup = 1;
sphere_max = 8;

repeat(clamp(floor(wave/4),1,3))
{
    var a = instance_create(x,y,obj_sphereMinion);
    a.hAccel = choose(-6,6);
    a.vAccel = choose(-6,6);
    a.parentKing = id;
    sphere_backup -= 1;
}

if raid_boss == true
{
    scale += 0.25; 
    accelRate += 0.01;
    spwnRate-=0.25;
}

//Prevent spawning inside tiles.
while place_meeting_fast(0,0,OBSTA) y -= 16;

image_xscale = scale;
image_yscale = scale;

#event destroy
event_inherited();

if dropItem != true then exit;

var r = irandom(4);
if r == 1
{
    scr_dropItem(ITEMID.weapon_sphereLauncher,0,ITEMTYPE.weapon,x,y,noone);
    scr_playSound(snd_rareItem,false,11,x,y,1);
} else if r > 2
{
    scr_dropTag("Tool Speed+",1);
    scr_playSound(snd_rareItem,false,11,x,y,1);
}

scr_spawnEssence(1,60,5+floor(wave/3));

#event alarm1|Reset hurt
canHurt = true;
target = obj_player;

#event alarm2|Reset state lock
stateLock = false;

objective = target;
state = WANDER;
image_index = sign(objective.x-x);

#event alarm3|Create a Sphere Minion
if !place_meeting(x,y,OBSTA)
{
    if sphere_backup >= 0
    {
        if instance_exists(obj_sphereMinion) && instance_number(obj_sphereMinion) > sphere_max
        {
            sphere_backup = 1;
        }
        else
        {
            var a = instance_create(x,y,obj_sphereMinion);
            a.hAccel = choose(-6,6);
            a.vAccel = choose(-6,6);
            a.parentKing = id;
            sphere_backup -= 1;
        }
    }
}
else sphere_backup += 1;

alarm[3] = room_speed*spwnRate;

#event step
//Pylon states
//TODO: If close to a playertile or something, enter WANDER. Like Bomb King.
if point_distance(x,y,obj_player.x,obj_player.y) < 10*16 //within 10 tiles
{
    target = obj_player;
} else target = obj_pie;


ai_shadowGremlin(MOVE,spr_gremlinIdle,0,spr_gremlinWalk,1,target,0,atkBox);
if speed != 0 then speed = approach(speed,0,1);

#event draw
draw_sprite_ext(sprite_index,image_index,x,y+(bobAmt*bobAltitude),image_xscale,image_yscale,image_angle,c_white,image_alpha);