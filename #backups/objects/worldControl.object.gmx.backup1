// 2020-09-23 17:40:03
#event create|Initialize world variables
globalvar grav,maxGrav,loadStep,wave,interm,enemyActiveList,RAIDBOUND_Upper,RAIDBOUND_Lower,raidProgress,maxRaidProgress,stoneLayer,specialRaid,nextRaid;
grav = 0.350;
maxGrav = 5;
loadStep = 0; //Load the world.
wave = 0; //Wave number
interm = true; //intermission boolean
enemyActiveList[0] = noone; //List of enemies that require chunking.
raidProgress = 0; //How many more gremlins need to die before the raid is complete.
maxRaidProgress = 100; //Raid goal.
stoneLayer = (room_height/2-48)+(10*16); //Around the layer of stone

if room == rm_zero 
{
    scr_loadWorld();
    scr_raidControl("INTERM",0,0,0,0); //Preset setting, other arguments not required.
    alarm[0] = 1;
    alarm[1] = 120;
    plr_xBuffer = 0;
    plr_yBuffer = 0;
}

//Local variables
world_Time = 0;
heal_Time = 10; //Heal something (defense) every interval.
playerTiles = 0;
view_zoom_buffer = 0; //Remember the last value of the zoom.
maxGrem = 5; //Maximum gremlins in the world.
spawnRate = 1; //How many times a second to check for and spawn a thing.
spawnChance = 0; //The chance that the thing will spawn out of 100.
raidBoss = false; //No raid boss.
raidBossID = noone; //The instance ID of the boss.
nextRaid = "RAID"; //The next raid's type.
specialRaid = false; //Bool telling whether or not a special raid is in progress.
special_raidID = noone; //The ID of the raid
world_TimeMax = room_speed*(3*60);
dayTransition = false; //Whether or not to transition into day;
dayAlpha = 0;

global.light_surface = surface_create(display_get_width(),display_get_height());


#event alarm0|Deactivation/Chunking and World Time
if interm == true 
{
    world_Time += 1;
    heal_Time += 1;
}

var playerX = obj_player.x;
var playerY = obj_player.y;
var view_zoom = camera.view_zoom;
var playerLight = obj_player.playerLight;

//----Deactivation---------
if view_zoom != view_zoom_buffer
{
    //Update view_zoom
    view_zoom = view_zoom_buffer;
    
    //-Deactivation and Reactivation updates every two tiles.
    if (abs(playerX-plr_xBuffer) > 32 || abs(playerY-plr_yBuffer) > 32)
    {
            
        //-Activate everything inside the camera view.
        var outerBoundary = 10*16; //-"offscreen" boundary distance from view. 10 tiles
        var enemyBoundary = 5*16; //-tiles enemdies are allowed to have active.
        instance_activate_region(view_xview[0]-outerBoundary,view_yview[0]-outerBoundary,450+2*(outerBoundary),450+2*(outerBoundary),true);
        
        //Keep saplings active.
        if instance_exists(obj_sapling)
        {   instance_activate_object(obj_sapling); }
        
        //-Deactivate offscreen objects.
        with obj_itemDrop scr_deactivateOffscreen(id);
        with TILE event_user(0);
        with INTERACT scr_deactivateOffscreen(id);
        with NOCOL scr_deactivateOffscreen(id);
        
        //-Update player buffer
        plr_xBuffer = round(playerX);
        plr_yBuffer = round(playerY);
        
        //-Activate offscreen tiles for needed enemies.
        if array_length_1d(enemyActiveList) > 0
        {
            for (i=0;i<array_length_1d(enemyActiveList);i++)
            {
                with enemyActiveList[i]
                {
                 instance_activate_region(x-enemyBoundary,y-enemyBoundary,x+enemyBoundary,y+enemyBoundary,true);
                }
            }
        }
    }
}

//-----------Light up underground tiles----------------
//Updates every tile.
if (abs(playerX-plr_xBuffer) > 15 || abs(playerY-plr_yBuffer) > 15)
{
    
    if playerY > stoneLayer-48
    {
        with TILE
        {
            if light_level < 255
            {
                var dist = point_distance(x,y,playerX,playerY);
                var light_dist = floor(255-dist*2.5);
                
                if dist < playerLight*2
                { light_level = 255;}
                else if light_dist <= 255 && light_dist > 0  { light_level = light_dist; }
                else light_level = 0;
            }
        }
        
        with obj_nullLight
        {
            if light_level < 255
            {
                var dist = point_distance(x,y,playerX,playerY);
                var light_dist = floor(255-dist*2.5);
                
                if dist < playerLight*2
                { instance_destroy(); }
                else if light_dist <= 255 && light_dist > 0  { light_level = light_dist; }
                else light_level = 0;
            }
        }
    }
}


//------------- Proliferate every tick ---------------------------
if heal_Time >= room_speed
{
    with PLRTILE event_user(15);
    with PLR_NOCOL event_user(15);
    with obj_pie event_user(1);
    if obj_player.hp != obj_player.maxHp && obj_player.dead == false with obj_player { hp += 1; if hp >= maxHp then hp = maxHp; }
    
    heal_Time = 0;
}

//------------ World Time -------------------
// multiplied by 3 into 3 minutes.

if world_Time >= world_TimeMax //3 minutes
{
    world_Time = 0;
    scr_raidControl(nextRaid,0,0,0,0); //Preset raid
}




alarm[0] = 2;

#event alarm1|Spawn gremlins

var _viewX = view_xview[0];
var _viewW = view_wview[0]+_viewX;
var _viewY = view_yview[0];
var _viewH = view_hview[0]+_viewY;


if instance_number(ENEMY) < maxGrem && irandom(100) <= spawnChance
{
    var outer_x_range = 2*16; //Defines how many tiles outside the xview the enemy can spawn.
    if interm == false then outer_x_range = 5;
    //var outer_y_range = irandom_range(1,5)*16; //Defines how many tiles outside the yview the enemy can spawn.
    
    var xx = choose(_viewX,_viewW);
    var yy = choose(_viewY,_viewH);
    var xdir = sign(xx-obj_player.x);
    
    //Change xx coordinate
    xx = xx+(xdir*irandom(outer_x_range));
      
    //Center points
    xx = floor(xx/16)*16;
    yy = floor(yy/16)*16;
    
    //Spawn mobs outside flat lands + (or during raids)
    var spawn_at_portal = false;
    
    if (xx >= RAIDBOUND_Lower && xx <= RAIDBOUND_Upper) || interm == false
    {
        xx = choose(RAIDBOUND_Lower-16,RAIDBOUND_Upper+16); 
        yy = (room_height/2)-(16*4);
        spawn_at_portal = !interm; //if interm == false then spawn_at_portal = true;
    }
    
    //Gremlins spawn on top of stuff
    while position_meeting(xx,yy,OBSTA) yy-=16;
    
    //Spawn gremlin
    var flyChance = 0.3;
    var gremNumber = random(1);
    
    //-Check whether or not the enemy will spawn inside the view.
    if !point_in_rectangle(xx,yy,_viewX,_viewY,_viewW,_viewH)
    {
        //--SpawnGremlins directly ontop of a tile from the sky
        while (!position_meeting(xx,yy+16,TILE_ALL) && !position_meeting(xx,yy+16,GREM_BLOCK)) { yy+=16; if yy > room_height then break; }
        
        new_raidGremlin(xx,yy,gremNumber,flyChance,special_raidID);
        
        //Special mob spawns.
            //-Sphere Pylon
        if interm == true
        {
            if irandom(50) >= 48
            {
                instance_create(xx,yy,obj_spherePylon);
            }
        }
    } else if spawn_at_portal == true { new_raidGremlin(xx,yy,gremNumber,flyChance,special_raidID); }
    
    //Clear active mob roster
    if array_length_1d(enemyActiveList) > 3 { enemyActiveList = 0; }
    
    
} else if instance_number(ENEMY) >= maxGrem
{
    //Despawn an offview gremlin.
    for (i=0;i<instance_number(ENEMY);i++)
    {
        var grem = instance_find(ENEMY,i);
        var gx = grem.x;
        
        //Don't despawn things inside the Flatlands
        if !(gx >= RAIDBOUND_Lower && gx <= RAIDBOUND_Upper)
        {
            //Check whether or not the gremlin is visible.
            if !point_in_rectangle(grem.x,grem.y,_viewX,_viewY,_viewW,_viewH)
            {
                with grem instance_destroy();
                break;
            }
        }
    }
}

alarm[1] = room_speed/spawnRate;

#event step|D/N Cycle Alpha
var aMax = 0.6;

if interm == true
{
    //Alpha = equation to get the darkness of the day until til night
    if dayTransition == true
    {
        if dayAlpha > 0 { dayAlpha -= 0.01; } else dayTransition = false;
    }
    else dayAlpha = ((world_Time/world_TimeMax));
    
    dayAlpha = clamp(dayAlpha,0,aMax);
} else if interm == false then dayAlpha = aMax;

#event other_user0|Event: RAID_PROGRESSED
print('Raid Progressed');

//Increase progress meter
if raidProgress <= maxRaidProgress
{
    if raidBoss == false { raidProgress += 1 }
    else if raidProgress >= 99 then raidProgress = 99;
}



//-------RAID DEFEATED EVENT---------

//Check for completion
if raidProgress >= maxRaidProgress
{
    print('Raid Complete');
    scr_raidControl("INTERM",0,0,0,0);
    
    //Grow trees
    if instance_exists(obj_sapling)
    { 
        with obj_sapling
        {
            var r = irandom(10);
            if r <= 7
            {
                scr_treeCreate(x,y);
                instance_destroy();
            }
        }
    }
}

#event draw_gui|Day and night cycle
draw_set_color(make_colour_hsv(240,100,17));
draw_set_alpha(dayAlpha);
draw_rectangle(0,0,display_get_width(),display_get_height(),false);
draw_set_alpha(1);

#event keyrelease:B
if dev == true then scr_raidControl(nextRaid,0,0,0,0);